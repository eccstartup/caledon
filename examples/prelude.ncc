---------------
-- builtins ---
---------------
defn char : atom  -- builtin

defn putChar    :  char -> atom -- builtin
   | putCharImp = [A] putChar A

-- for sequencing io actions
fixity left 1 ,
defn io : atom
   | do = io
   | ,  = io -> atom -> io 

defn run : io -> atom
   | runDo = run do
   | runSeq = [A][B] run (A , B) <- run A 
                     	         <- B

defn readLine    : (string -> io) -> atom -- builtin
   | readLineImp = [Foo : string -> io] [A : string] readLine Foo <- run (Foo A)

defn string : atom
  as list char

---------------
-- searches ---
---------------
defn any : {A : atom} (A -> atom) -> atom
   | is = [A : atom][V : A][F : A -> atom] F V -> any { A = A } F

fixity lambda findOne 
defn findOne : [A : atom] (A -> atom) -> atom
  as \a: atom . any { A = a }

--------------------------
--- useful combinators ---
--------------------------
fixity right 0 $
defn $ : {a b:atom} (a -> b) -> a -> b
  as ?\ at bt . \ f . \a . f a


fixity right 0 @
defn @ : {a b c:atom} (b -> c) -> (a -> b) -> a -> c
  as ?\at bt ct : atom . \f : bt -> ct . \ g : at -> bt . \ a : at . f (g a)

defn flip : {a b c : atom} (a -> b -> c) -> b -> a -> c
  as ?\ at bt ct : atom . \ foo . \ b . \ a . foo a b

-------------------
--- Constraints ---
-------------------
fixity none 1 =:=
defn =:= : {q} q -> q -> atom
   | eq = [a : atom][b:a] (=:=) {q = a} b b

-- searching for these is SLOW
fixity none 0 /\
defn /\ : atom -> atom -> atom
   | and = [a b:atom] a -> b -> a /\ b

fixity none 0 \/
defn \/ : atom -> atom -> atom
   | or1 = [a b:atom] a -> a \/ b
   | or2 = [a b:atom] b -> a \/ b

--------------
--- concat ---
--------------
defn concatable : atom -> atom
   | concatableImp = [M] (M -> M -> M -> atom) -> concatable M

fixity right 3 ++
defn ++ : {M} {cm : concatable M} M -> M -> M -> atom
   | ppimp = [M][Foo : M -> M -> M -> atom][M1 M2 M3 : M] (++) {M = M} {cm = concatableImp M Foo} M1 M2 M3 <- Foo M1 M2 M3

-------------
--- Order ---
-------------

defn orderable : atom -> atom
   | orderableImp = [M] (M -> M -> atom) -> orderable M

fixity right 3 ≤ 
defn ≤ : {M} {cm : orderable M} M -> M -> atom
   | ooimp = [M][Foo : M -> M -> atom][M1 M2 : M] (≤) {M = M} {cm = orderableImp M Foo} M1 M2 <- Foo M1 M2

fixity none 2 ≥
defn ≥ : {M : atom} {cm : orderable M} M -> M -> atom
  as ?\M . ?\ cm : orderable M . \M1 M2 : M . M2 ≤ M1

---------------------
--- Unary Numbers ---
---------------------
defn natural  : atom
   | zero = natural
   | succ = natural -> natural

defn add   : natural -> natural -> natural -> atom
   | add_z = [N] add zero N N
   | add_s = [N M R] add N M R -> add (succ N) M (succ R)

-- sub N M R is N - M = R
defn sub   : natural -> natural -> natural -> atom
   | sub_by_add = [N M R] sub N M R <- add M R N

defn concatableNatural : concatable natural
  as concatableImp natural add

defn lte-nat : natural -> natural -> atom
   | leqZero = [B] lte-nat zero B
   | leqSucc = [A B] lte-nat (succ A) (succ B) <- lte-nat A B

fixity none 3 <
defn < : natural -> natural -> atom
   | ltZero = [B] zero < succ B
   | ltSucc = [A B] succ A < succ B <- A < B

defn orderableNatural : orderable natural
  as orderableImp natural lte-nat

query findSat1 = exists A . A =:= zero

-- query add1 = (++) (succ zero) zero (succ zero)

-- query add2 = exists A : natural . add (succ zero) zero A

-- query add3 = any $ add (succ zero) zero

-- query findSat2 = succ zero ≤ succ (succ zero) /\ succ zero ≤ succ zero

-- defn pred : natural -> atom
--   | predimp = [A : natural] zero < A -> A < succ (succ zero) -> pred A

-- query findSat3 = exists A: natural.  pred A


-------------
--- Maybe ---
-------------

defn maybe : atom -> atom
   | nothing = {a} maybe a
   | just = {a} a -> maybe a

-------------
--- Lists ---
-------------
defn list : atom -> atom
   | nil  = {a} list a
   | cons = {a} a -> list a -> list a

defn concatList : {A} list A -> list A -> list A -> atom
   | concatListNil  = [T][L:list T] concatList {A = T} nil L L
   | concatListCons = [T][A B C : list T][V:T] concatList (cons V A) B (cons V C) <- concatList A B C

defn concatableList : {A:atom} concatable (list A)
  as ?\A . concatableImp (list A) concatList

----------------
--- printing ---
----------------

defn putStr : string -> atom
   | putStr_Nil = putStr $ nil {a = char}
   | putStr_Cons = [v:char][l: list char] 
                   putStr $ cons {a = char} v l 
                <- putChar v
                <- putStr l

----------------
--- Booleans ---
----------------
defn bool : atom
   | true = bool
   | false = bool

defn if : bool -> bool
  as \b . b

fixity none 1 |:|
defn |:| : {a:atom} a -> a -> (a -> a -> a) -> a
  as ?\t : atom . \a b : t. \f : t -> t -> t. f a b


fixity none 0 ==>
defn ==> : {a : atom} bool -> ((a -> a -> a) -> a) -> a -> atom
   | thentrue  = [a : atom][f: _ -> a] (true ==> f)  (f (\A B : a. A))
   | thenfalse = [b : atom][f: _ -> b] (false ==> f) (f (\A B : b . B))

defn not : bool -> bool -> atom
  as \a . if a ==> false |:| true

query main = run $ do 
                 , putStr "hey!\n"
	  	 , readLine ( \A . do 
   		 , putStr A
                 , putStr "\nbye!\n")
