-------------
--- Lists ---
-------------
defn list : prop -> prop
   | nil  = list A
   | cons = A -> list A -> list A

defn concatList : list A -> list A -> list A -> prop
  >| concatListNil  = concatList {A = T} nil L L
  >| concatListCons = concatList {A = T} (cons V A) B (cons V C) <- concatList {A = T} A B C


defn churchList : prop -> prop
  as \A : prop . [lst : prop] lst -> (A -> lst -> lst) -> lst

defn consCL : [B : prop] B -> churchList B -> churchList B
  as \C : prop . 
     \V : C . 
     \cl : churchList C .
     \lst : prop.
     \nil : lst. 
     \cons : C -> lst -> lst . 
     cons V (cl lst nil cons)

defn nilCL : [B : prop] churchList B
  as \C : prop . 
     \lst : prop.
     \nil : lst. 
     \cons : C -> lst -> lst .
     nil

defn mapCL : {A B} (A -> B) -> churchList A -> churchList B
  as ?\A B : prop.
     \F : A -> B . 
     \cl : churchList A .
     \lst : prop.
     \nil : lst .
     \cons : B -> lst -> lst.
     cl lst nil (\v . cons (F v))

defn foldrCL : {A B} (A -> B -> A) -> A -> churchList B -> A
  as ?\A B : prop.
     \F : A -> B -> A. 
     \bc : A .
     \cl : churchList B .
     cl A bc (\v : B . \c : A . F c v)
